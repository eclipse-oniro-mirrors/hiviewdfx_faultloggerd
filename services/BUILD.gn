# Copyright (c) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

faultloggerd_sources = [
  "epoll_manager.cpp",
  "fault_logger_config.cpp",
  "fault_logger_daemon.cpp",
  "fault_logger_server.cpp",
  "fault_logger_service.cpp",
  "temp_file_manager.cpp",
  "fault_common_util.cpp"
]

if (defined(ohos_lite)) {
  copy("faultlogger.cfg") {
    sources = [ "$faultloggerd_path/services/config/faultloggerd.cfg" ]
    outputs = [ "$root_out_dir/etc/init/faultloggerd.cfg" ]
  }

  copy("faultlogger.conf") {
    sources = [ "$faultloggerd_path/services/config/faultlogger.conf" ]
    outputs = [ "$root_out_dir/etc/faultlogger.conf" ]
  }

  executable("faultloggerd") {
    visibility = [ "*:*" ]

    include_dirs = [
      ".",
      "$c_utils_include_path",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$hilog_lite_include_path",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client",
      "$faultloggerd_interfaces_path/innerkits/procinfo/include",
    ]

    sources = faultloggerd_sources
    sources += [
      "$c_utils_src_path/directory_ex.cpp",
      "main.cpp",
    ]

    cflags = [ "-fstack-protector-strong" ]

    deps = [
      ":faultlogger.conf",
      "$faultloggerd_common_path/trace:dfx_trace",
      "$faultloggerd_interfaces_path/innerkits/procinfo:libdfx_procinfo",
      "$faultloggerd_path/common/dfxlog:dfx_hilog",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
    ]

    external_deps = [ "hilog_lite:hilog_shared" ]

    defines = [ "is_ohos_lite" ]
    if (faultloggerd_hisysevent_enable) {
      deps += [ "hisysevent:libhisysevent" ]
    } else {
      defines += [ "HISYSEVENT_DISABLE" ]
    }
  }
} else {
  faultloggerd_sources += [
    "./coredump/coredump_callback_service.cpp",
    "./coredump/coredump_facade.cpp",
    "./coredump/coredump_manager_service.cpp",
    "./coredump/coredump_session_manager.cpp",
    "./coredump/coredump_session_service.cpp",
    "./coredump/coredump_session_state.cpp",
    "./coredump/coredump_signal_service.cpp",
    "./coredump/coredump_task_scheduler.cpp",
    "fault_logger_pipe.cpp",
    "kernel_snapshot_task.cpp",
  ]

  config("faultloggerd_config") {
    visibility = [ ":*" ]

    include_dirs = [
      ".",
      "./coredump",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client",
      "$faultloggerd_path/interfaces/innerkits/signal_handler",
      "$faultloggerd_interfaces_path/innerkits/procinfo/include",
    ]
  }

  ohos_prebuilt_etc("faultloggerd.cfg") {
    if (use_musl) {
      source = "config/faultloggerd.cfg"
    } else {
      source = "config/bionic/faultloggerd.cfg"
    }

    relative_install_dir = "init"
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_prebuilt_etc("faultloggerd_config.json") {
    source = "config/faultloggerd_config.json"
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_prebuilt_etc("faultloggerd.para") {
    source = "config/faultloggerd.para"
    relative_install_dir = "param"
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_prebuilt_etc("faultloggerd.para.dac") {
    source = "config/faultloggerd.para.dac"
    relative_install_dir = "param"
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_prebuilt_etc("faultlogger.conf") {
    source = "config/faultlogger.conf"
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_prebuilt_etc("fault_coredump.json") {
    source = "config/fault_coredump.json"
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_executable("faultloggerd") {
    install_enable = true
    configs = [
      ":faultloggerd_config",
      "$faultloggerd_path/common/build:coverage_flags",
    ]
    sources = faultloggerd_sources
    sources += [ "main.cpp" ]
    cflags = [ "-fstack-protector-strong" ]
    cflags_cc = [ "-DDEBUG_CRASH_LOCAL_HANDLER" ]
    defines = [ "DFX_ENABLE_TRACE" ]

    deps = [
      ":faultlogger.conf",
      ":faultloggerd.para",
      ":faultloggerd.para.dac",
      ":faultloggerd_config.json",
      ":fault_coredump.json",
      "$faultloggerd_frameworks_path/localhandler:dfx_local_handler_src",
      "$faultloggerd_interfaces_path/innerkits/procinfo:libdfx_procinfo",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/common/trace:dfx_trace",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
    ]

    if (!is_asan) {
      deps += [ ":faultloggerd.cfg" ]
    } else {
      defines += [ "DFX_ALLOCATE_ASAN" ]
    }

    external_deps = [
      "bounds_checking_function:libsec_shared",
      "cJSON:cjson",
      "c_utils:utils",
      "hilog:libhilog",
      "hisysevent:libhisysevent",
      "hitrace:hitrace_meter",
      "init:libbegetutil",
    ]
    version_script = "faultloggerd.map"
    install_images = [
      "system",
      "updater",
    ]
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }
}
