# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//build/lite/config/test.gni")

  if (ohos_build_type == "debug") {
    unittest("test_common") {
      output_extension = "bin"
      output_dir = "$root_out_dir/test/unittest/faultloggerd"

      include_dirs = [
        "$c_utils_include_path",
        "$hilog_lite_include_path",
        "$faultloggerd_interfaces_path/common",
        "$faultloggerd_path/common/dfxlog",
        "$faultloggerd_path/common/dfxutil",
      ]

      sources = [ "common_test.cpp" ]

      deps = [
        "$faultloggerd_path/common/dfxlog:dfx_hilog",
        "$faultloggerd_path/common/dfxutil:dfx_util",
        "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
        "$hilog_lite_deps_path",
      ]
    }
  }

  group("unittest") {
    deps = [ ":test_common" ]
  }
} else {
  import("//build/config/features.gni")
  import("//build/test.gni")

  config("module_private_config") {
    visibility = [ ":*" ]

    include_dirs = [
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common/cutil",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
    ]
  }

  module_output_path = "faultloggerd/common"

  ohos_unittest("test_common") {
    module_out_path = module_output_path
    sources = [
      "$faultloggerd_path/common/dfxlog/dfx_log.cpp",
      "common_cutil_test.cpp",
      "common_test.cpp",
    ]
    defines = [ "DFX_LOG_DMESG" ]
    configs = [ ":module_private_config" ]

    deps = [
      "$faultloggerd_path/common/cutil:dfx_cutil",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "//third_party/googletest:gmock_main",
      "//third_party/googletest:gtest_main",
    ]
    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
    ]
  }

  ohos_unittest("test_param") {
    module_out_path = module_output_path
    sources = [ "param_test.cpp" ]
    configs = [ ":module_private_config" ]

    defines = [ "ENABLE_PARAMETER" ]

    deps = [
      "//third_party/googletest:gmock_main",
      "//third_party/googletest:gtest_main",
    ]
    external_deps = [
      "c_utils:utils",
      "init:libbegetutil",
    ]
  }

  group("unittest") {
    testonly = true
    deps = [ ":test_common" ]
    if (!defined(ohos_lite)) {
      deps += [ ":test_param" ]
    }
  }
}
