# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//build/lite/config/test.gni")

  if (ohos_build_type == "debug") {
    unittest("test_dumpcatcher") {
      output_extension = "bin"

      output_dir = "$root_out_dir/test/unittest/faultloggerd/faultloggerd"

      sources = [ "dumpcatcher_interfaces_test.cpp" ]

      include_dirs = [
        "include/",
        "$faultloggerd_interfaces_path/common",
        "$faultloggerd_path/interfaces/innerkits/backtrace/include",
        "$faultloggerd_path/interfaces/innerkits/dump_catcher/include",
        "$faultloggerd_path/interfaces/innerkits/dump_catcher",
        "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
        "$faultloggerd_path/interfaces/innerkits/formatter/include",
        "$faultloggerd_path/test/utils",
        "$faultloggerd_path/tools/process_dump",
        "$faultloggerd_path/utils",
        "$hilog_lite_include_path",
      ]

      deps = [
        "$faultloggerd_path/interfaces/innerkits/dump_catcher:libdfx_dumpcatcher",
        "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
        "$faultloggerd_path/test/utils:dfx_test_util",
      ]

      external_deps = [ "hilog_lite:hilog_shared" ]
    }
  }

  group("unittest") {
    deps = [ ":test_dumpcatcher" ]
  }
} else {
  import("//build/config/features.gni")
  import("//build/test.gni")

  module_output_path = "faultloggerd/faultloggerd/dump_catcher"

  ohos_unittest("test_dumpcatcher") {
    module_out_path = module_output_path

    visibility = [ "*:*" ]

    include_dirs = [
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/interfaces/innerkits/backtrace/include",
      "$faultloggerd_path/interfaces/innerkits/dump_catcher/include",
      "$faultloggerd_path/interfaces/innerkits/dump_catcher",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
      "$faultloggerd_path/interfaces/innerkits/formatter/include",
      "$faultloggerd_path/test/utils",
    ]

    sources = [
      "$faultloggerd_path/interfaces/innerkits/dump_catcher/kernel_stack_async_collector.cpp",
      "dumpcatcher_command_test.cpp",
      "dumpcatcher_interfaces_test.cpp",
      "kernelstack_async_collector_test.cpp",
    ]
    cflags_cc = [ "-Dprivate=public" ]

    deps = [
      "$faultloggerd_common_path/dfxlog:dfx_hilog_base",
      "$faultloggerd_interfaces_path/innerkits/backtrace:libbacktrace_local",
      "$faultloggerd_interfaces_path/innerkits/formatter:libjson_stack_formatter",
      "$faultloggerd_path/interfaces/innerkits/dump_catcher:libdfx_dumpcatcher",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
      "$faultloggerd_path/interfaces/innerkits/procinfo:libdfx_procinfo",
      "$faultloggerd_path/test/utils:dfx_test_util",
    ]
    if (support_jsapi) {
      deps += [ "$faultloggerd_path/test/resource:FaultloggerdJsTest" ]
    }
    resource_config_file = "$faultloggerd_path/test/resource/ohos_test.xml"
    external_deps = [
      "c_utils:utils",
      "googletest:gtest_main",
      "hilog:libhilog",
      "jsoncpp:jsoncpp",
    ]
  }

  group("unittest") {
    testonly = true
    deps = [ ":test_dumpcatcher" ]
  }
}
