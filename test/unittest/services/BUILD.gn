# Copyright (c) 2021-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//build/lite/config/test.gni")

  if (ohos_build_type == "debug") {
    unittest("test_service") {
      output_extension = "bin"
      output_dir = "$root_out_dir/test/unittest/faultloggerd"

      include_dirs = [
        "$c_utils_include_path",
        "$faultloggerd_path/services",
        "$hilog_lite_include_path",
        "$faultloggerd_path/tools/process_dump",
        "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
        "$faultloggerd_path/interfaces/innerkits/faultloggerd_client",
        "$faultloggerd_interfaces_path/common",
        "$faultloggerd_path/common/dfxlog",
        "$faultloggerd_path/common/dfxutil",
      ]

      sources = [
        "$faultloggerd_path/services/fault_logger_config.cpp",
        "$faultloggerd_path/services/fault_logger_daemon.cpp",
        "$faultloggerd_path/services/fault_logger_pipe.cpp",
        "faultlogger_config_test.cpp",
        "faultlogger_daemon_test.cpp",
        "faultlogger_pipe_test.cpp",
      ]
      sources += [ "$c_utils_src_path/directory_ex.cpp" ]

      deps = [
        "$faultloggerd_common_path/trace:dfx_trace",
        "$faultloggerd_path/common/dfxlog:dfx_hilog",
        "$faultloggerd_path/common/dfxutil:dfx_util",
        "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
        "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      ]

      if (faultloggerd_hisysevent_enable) {
        deps += [ "hisysevent:libhisysevent" ]
      } else {
        defines = [ "HISYSEVENT_DISABLE" ]
      }
    }
  }

  group("unittest") {
    deps = [ ":test_service" ]
  }
} else {
  import("//build/config/features.gni")
  import("//build/test.gni")

  config("module_private_config") {
    visibility = [ ":*" ]

    include_dirs = [
      "$faultloggerd_path/services",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client",
    ]
  }

  ohos_unittest("test_service") {
    module_out_path = "faultloggerd/services"
    sources = [
      "$faultloggerd_path/services/fault_logger_config.cpp",
      "$faultloggerd_path/services/fault_logger_daemon.cpp",
      "$faultloggerd_path/services/fault_logger_pipe.cpp",
      "faultlogger_config_test.cpp",
      "faultlogger_daemon_test.cpp",
      "faultlogger_pipe_test.cpp",
    ]

    configs = [
      ":module_private_config",
      "$faultloggerd_path/common/build:coverage_flags",
    ]

    deps = [
      "$faultloggerd_common_path/trace:dfx_trace",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
      "//third_party/googletest:gmock_main",
      "//third_party/googletest:gtest_main",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "hisysevent:libhisysevent",
    ]
  }

  group("unittest") {
    testonly = true
    deps = [ ":test_service" ]
  }
}
