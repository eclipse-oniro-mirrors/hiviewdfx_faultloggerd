# Copyright (c) 2023-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

dfx_test_util_sources = [ "dfx_test_util.cpp" ]

faultloggerd_test_source = [
  "$faultloggerd_interfaces_path/innerkits/faultloggerd_client/faultloggerd_client.cpp",
  "$faultloggerd_interfaces_path/innerkits/faultloggerd_client/faultloggerd_socket.cpp",
  "$faultloggerd_services_path/epoll_manager.cpp",
  "$faultloggerd_services_path/fault_logger_config.cpp",
  "$faultloggerd_services_path/fault_logger_daemon.cpp",
  "$faultloggerd_services_path/fault_logger_server.cpp",
  "$faultloggerd_services_path/fault_logger_service.cpp",
  "$faultloggerd_services_path/temp_file_manager.cpp",
  "$faultloggerd_services_path/fault_common_util.cpp",
  "faultloggerd_test_server.cpp"
]

if (defined(ohos_lite)) {
  static_library("dfx_test_util") {
    visibility = [ "*:*" ]
    include_dirs = [
      ".",
      "$c_utils_include_path",
      "$hilog_lite_include_path",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common/dfxutil",
    ]
    sources = dfx_test_util_sources
    deps = [ "$hilog_lite_deps_path" ]
  }

  static_library("faultloggerd_test_src") {
    visibility = [ "*:*" ]
    include_dirs = [
      "$c_utils_include_path",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_interfaces_path/innerkits/procinfo/include",
      "$faultloggerd_path/common/cutil",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
      "$faultloggerd_path/interfaces/innerkits/signal_handler",
      "$faultloggerd_services_path/",
    ]

    faultloggerd_test_source += [ "$c_utils_src_path/directory_ex.cpp" ]

    sources = faultloggerd_test_source

    deps = [
      "$faultloggerd_interfaces_path/innerkits/procinfo:libdfx_procinfo",
      "$faultloggerd_path/common/dfxlog:dfx_hilog",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/common/cutil:dfx_cutil",
      "$faultloggerd_path/common/trace:dfx_trace",
    ]

    external_deps = [
      "bounds_checking_function:libsec_shared",
      "init:libbegetutil",
    ]
    defines = [
      "is_ohos_lite",
      "FAULTLOGGERD_TEST",
    ]

    if (faultloggerd_hisysevent_enable) {
      deps += [ "hisysevent:libhisysevent" ]
    } else {
      defines += [ "HISYSEVENT_DISABLE" ]
    }
  }

} else {
  ohos_static_library("dfx_test_util") {
    visibility = [ "*:*" ]
    include_dirs = [
      ".",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common/dfxutil",
    ]
    sources = dfx_test_util_sources
    external_deps = [ "c_utils:utils" ]
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  faultloggerd_test_source += [
    "$faultloggerd_services_path/coredump/coredump_callback_service.cpp",
    "$faultloggerd_services_path/coredump/coredump_facade.cpp",
    "$faultloggerd_services_path/coredump/coredump_manager_service.cpp",
    "$faultloggerd_services_path/coredump/coredump_session_manager.cpp",
    "$faultloggerd_services_path/coredump/coredump_session_service.cpp",
    "$faultloggerd_services_path/coredump/coredump_session_state.cpp",
    "$faultloggerd_services_path/coredump/coredump_signal_service.cpp",
    "$faultloggerd_services_path/coredump/coredump_task_scheduler.cpp",
    "$faultloggerd_services_path/fault_logger_pipe.cpp",
    "$faultloggerd_services_path/kernel_snapshot_task.cpp",
  ]

  config("faultloggerd_config") {
    visibility = [ ":*" ]
    include_dirs = [
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_interfaces_path/innerkits/procinfo/include",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
      "$faultloggerd_path/interfaces/innerkits/signal_handler",
      "$faultloggerd_services_path/",
      "$faultloggerd_services_path/coredump",
    ]
  }
  ohos_static_library("faultloggerd_test_src") {
    configs = [ ":faultloggerd_config" ]

    defines = [ "FAULTLOGGERD_TEST" ]

    sources = faultloggerd_test_source

    deps = [
      "$faultloggerd_frameworks_path/localhandler:dfx_local_handler_src",
      "$faultloggerd_interfaces_path/innerkits/procinfo:libdfx_procinfo",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/common/trace:dfx_trace",
    ]

    external_deps = [
      "bounds_checking_function:libsec_shared",
      "cJSON:cjson",
      "c_utils:utils",
      "hilog:libhilog",
      "hisysevent:libhisysevent",
      "hitrace:hitrace_meter",
      "init:libbegetutil",
    ]
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }
}
