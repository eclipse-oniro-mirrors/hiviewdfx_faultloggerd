# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

processdump_sources = [
  "$faultloggerd_frameworks_path/unwinder/dfx_config.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_elf.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_frame_format.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_maps.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_memory.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_memory_file.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_regs.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_regs_arm.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_regs_arm64.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_regs_x86_64.cpp",
  "$faultloggerd_frameworks_path/unwinder/dfx_symbols.cpp",
  "dfx_fault_stack.cpp",
  "dfx_logger.cpp",
  "dfx_process.cpp",
  "dfx_ring_buffer_wrapper.cpp",
  "dfx_signal.cpp",
  "dfx_thread.cpp",
  "dfx_unwind_remote.cpp",
  "printer.cpp",
  "process_dumper.cpp",
]

if (defined(ohos_lite)) {
  source_set("process_info_src") {
    cflags = []

    defines = [
      "HAVE_CONFIG_H",
      "HAVE_ELF_H",
      "HAVE_LINK_H",
    ]

    visibility = [ "*:*" ]

    include_dirs = [
      ".",
      "$c_utils_include_path",
      "$faultloggerd_frameworks_path/unwinder/include",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
      "$hilog_lite_include_path",
    ]

    sources = processdump_sources

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/libunwind:libunwind",
    ]
  }

  executable("processdump") {
    visibility = [ "*:*" ]

    include_dirs = [
      ".",
      "$c_utils_include_path",
      "$faultloggerd_frameworks_path/unwinder/include",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/utils",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
      "$hilog_lite_include_path",
    ]

    defines = [
      "HAVE_CONFIG_H",
      "HAVE_ELF_H",
      "HAVE_LINK_H",
    ]

    sources = processdump_sources
    sources += [
      "cppcrash_reporter.cpp",
      "main.cpp",
    ]

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/libunwind:libunwind",
    ]
  }
} else {
  config("processdump_config") {
    visibility = [ ":*" ]

    include_dirs = [
      ".",
      "$faultloggerd_frameworks_path/unwinder/include",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/rust/rustc_demangle",
    ]

    cflags_cc = [ "-DRUSTC_DEMANGLE" ]
  }

  ohos_source_set("process_info_src") {
    cflags = []

    configs = [ ":processdump_config" ]
    sources = processdump_sources

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
      "$faultloggerd_path/interfaces/rust/rustc_demangle:rustc_demangle",
      "//third_party/libunwind:libunwind",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog_native:libhilog",
    ]

    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_executable("processdump") {
    install_enable = true
    configs = [ ":processdump_config" ]
    sources = processdump_sources
    sources += [
      "cppcrash_reporter.cpp",
      "main.cpp",
    ]

    cflags_cc = [ "-DDEBUG_CRASH_LOCAL_HANDLER" ]

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
      "$faultloggerd_path/interfaces/innerkits/signal_handler:dfx_local_handler",
      "$faultloggerd_path/interfaces/rust/rustc_demangle:rustc_demangle",
      "//third_party/libunwind:libunwind",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog_native:libhilog",
    ]

    install_images = [
      "system",
      "updater",
    ]

    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }
}
