# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

dfx_unwinder_sources = [
  "$faultloggerd_path/tools/process_dump/dfx_symbols_cache.cpp",
  "arch_util.cpp",
  "dfx_config.cpp",
  "dfx_frame_format.cpp",
  "dfx_regs.cpp",
  "dfx_regs_arm.cpp",
  "dfx_regs_arm64.cpp",
  "dfx_regs_x86_64.cpp",
  "dwarf_unwinder.cpp",
  "fp_unwinder.cpp",
]

if (defined(ohos_lite)) {
  source_set("dfx_unwinder_src") {
    visibility = [ "*:*" ]

    include_dirs = [
      "include",
      "$c_utils_include_path",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/backtrace/include",
      "$faultloggerd_path/tools/process_dump",
      "//third_party/libunwind/include",
      "//third_party/libunwind/src",
      "$hilog_lite_include_path",
    ]

    sources = dfx_unwinder_sources
    sources += [ "$c_utils_src_path/directory_ex.cpp" ]

    defines = [
      "HAVE_CONFIG_H",
      "HAVE_ELF_H",
      "HAVE_LINK_H",
    ]

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$hilog_lite_deps_path",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/libunwind:libunwind_local",
    ]
  }
} else {
  config("dfx_unwinder_config") {
    visibility = [ "*:*" ]

    include_dirs = [
      "include",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/backtrace/include",
      "$faultloggerd_path/tools/process_dump",
      "//third_party/libunwind/include",
      "//third_party/libunwind/src",
    ]

    defines = [
      "HAVE_CONFIG_H",
      "HAVE_ELF_H",
      "HAVE_LINK_H",
    ]
  }

  ohos_source_set("dfx_unwinder_src") {
    public_configs = [ ":dfx_unwinder_config" ]

    sources = dfx_unwinder_sources

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/libunwind:libunwind_local",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog_native:libhilog",
    ]
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_source_set("dfx_unwinder_static_src") {
    public_configs = [ ":dfx_unwinder_config" ]

    sources = dfx_unwinder_sources
    if (target_cpu == "arm") {
      sources += [ "$faultloggerd_path/tools/process_dump/dfx_regs_arm.cpp" ]
    } else if (target_cpu == "arm64") {
      sources += [ "$faultloggerd_path/tools/process_dump/dfx_regs_arm64.cpp" ]
    } else if (target_cpu == "x86_64") {
      sources += [ "$faultloggerd_path/tools/process_dump/dfx_regs_x86_64.cpp" ]
    }

    deps = [
      "$faultloggerd_path/common/dfxlog:static_hilog_base",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/libunwind:libunwind_local",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog_native:libhilog_base",
    ]
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }
}
