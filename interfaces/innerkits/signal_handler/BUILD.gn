# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

if (defined(ohos_lite)) {
  shared_library("dfx_signalhandler") {
    visibility = [ "*:*" ]

    cflags = [
      "-fPIC",
      "-Wall",
      "-fno-builtin",
      "-DDFX_NO_PRINT_LOG",
    ]

    include_dirs = [
      "include",
      "//utils/native/base/include",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
    ]
    sources = [ "dfx_signal_handler.c" ]

    deps = [
      "$faultloggerd_path/common/util:dfx_cutil",
      "//third_party/bounds_checking_function:libsec_shared",
    ]
  }

  ohos_static_library("dfxsignalhandler") {
    cflags = [ "-DDFX_NO_PRINT_LOG" ]

    include_dirs = [
      "include",
      "//utils/native/base/include",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
    ]

    sources = [ "dfx_signal_handler.c" ]

    deps = [
      "$faultloggerd_path/common/util:dfx_cutil",
    ]
  }
} else {
  config("dfx_signal_handler_config") {
    visibility = [ "*:*" ]

    include_dirs = [
      "include",
      "//utils/native/base/include",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/tools/process_dump",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client/include",
    ]
  }

  ohos_static_library("dfx_local_handler") {
    sources = [
      "dfx_crash_local_handler.c",
      "dfx_signal_local_handler.c",
    ]
    public_configs = [ ":dfx_signal_handler_config" ]

    deps = [
      "$faultloggerd_path/common/dfxlog:static_hilog_base",
      "$faultloggerd_path/common/util:dfx_cutil",
      "$faultloggerd_path/interfaces/innerkits/faultloggerd_client:libfaultloggerd",
      "//third_party/libunwind:libunwind",
    ]

    external_deps = [
      "hilog_native:libhilog_base",
      "utils_base:utils",
    ]

    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_shared_library("dfx_signalhandler") {
    public_configs = [ ":dfx_signal_handler_config" ]

    cflags = [
      "-DDFX_LOG_USE_HILOG_BASE",
      "-DCRASH_LOCAL_HANDLER",
    ]

    sources = [ "dfx_signal_handler.c" ]

    deps = [
      ":dfx_local_handler",
      "$faultloggerd_path/common/dfxlog:static_hilog_base",
      "$faultloggerd_path/common/util:dfx_cutil",
    ]

    external_deps = [
      "hilog_native:libhilog_base",
      "utils_base:utils",
    ]

    install_images = [
      "system",
      "updater",
    ]

    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_static_library("dfxsignalhandler") {
    cflags = [ "-DDFX_NO_PRINT_LOG" ]

    include_dirs = [
      "include",
      "//utils/native/base/include",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
    ]

    sources = [ "dfx_signal_handler.c" ]

    deps = [
      "$faultloggerd_path/common/util:dfx_cutil",
    ]

    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }
}
