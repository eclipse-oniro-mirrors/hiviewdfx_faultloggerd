# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/hiviewdfx/faultloggerd/faultloggerd.gni")

backtrace_local_sources = [
  "backtrace_local.cpp",
  "backtrace_local_context.cpp",
  "backtrace_local_thread.cpp",
  "catchframe_local.cpp",
]

if (defined(ohos_lite)) {
  shared_library("libbacktrace_local") {
    visibility = [ "*:*" ]

    include_dirs = [
      "include",
      "$c_utils_include_path",
      "$faultloggerd_frameworks_path/unwinder/include",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/interfaces/innerkits/procinfo/include",
      "//third_party/libunwind/include",
      "//third_party/libunwind/src",
    ]

    if (target_cpu == "arm") {
      include_dirs += [ "//third_party/libunwind/include/tdep-arm" ]
    } else if (target_cpu == "arm64") {
      include_dirs += [ "//third_party/libunwind/include/tdep-aarch64" ]
    } else if (target_cpu == "x64" || target_cpu == "x86_64") {
      include_dirs += [ "//third_party/libunwind/include/tdep-x86_64" ]
    } else if (target_cpu == "mipsel") {
      include_dirs += [ "//third_party/libunwind/include/tdep-mips" ]
    }

    sources = backtrace_local_sources
    sources += [
      "$faultloggerd_frameworks_path/unwinder/arch_util.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_config.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_elf.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_frame_format.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_memory.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_memory_file.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_symbols.cpp",
      "$faultloggerd_frameworks_path/unwinder/dwarf_unwinder.cpp",
      "$faultloggerd_frameworks_path/unwinder/fp_unwinder.cpp",
    ]
    sources += [
      "$faultloggerd_frameworks_path/unwinder/dfx_regs.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_regs_arm.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_regs_arm64.cpp",
      "$faultloggerd_frameworks_path/unwinder/dfx_regs_x86_64.cpp",
    ]

    defines = [
      "HAVE_CONFIG_H",
      "HAVE_ELF_H",
      "HAVE_LINK_H",
    ]

    deps = [
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/procinfo:libdfx_procinfo",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/libunwind:libunwind",
    ]
  }

  static_library("backtrace_local") {
    # current static library is unuseful in ohos_lite type
  }
} else {
  config("dfx_backtrace_config") {
    visibility = [ "*:*" ]

    include_dirs = [
      "include",
      "$faultloggerd_interfaces_path/common",
      "$faultloggerd_interfaces_path/innerkits/procinfo/include",
      "$faultloggerd_path/common/dfxutil",
      "$faultloggerd_path/common",
    ]

    defines = [
      "HAVE_CONFIG_H",
      "HAVE_ELF_H",
      "HAVE_LINK_H",
    ]
  }

  ohos_shared_library("libbacktrace_local") {
    public_configs = [ ":dfx_backtrace_config" ]

    sources = backtrace_local_sources

    deps = [
      "$faultloggerd_frameworks_path/unwinder:dfx_unwinder_src",
      "$faultloggerd_path/common/dfxlog:static_common_log",
      "$faultloggerd_path/common/dfxutil:dfx_util",
      "$faultloggerd_path/interfaces/innerkits/procinfo:libdfx_procinfo",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/libunwind:libunwind",
    ]
    include_dirs = [
      "$faultloggerd_frameworks_path/unwinder/include",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "//third_party/libunwind/include",
      "//third_party/libunwind/include/tdep/",
      "//third_party/libunwind/src",
    ]
    version_script = "libbacktrace_local.map"
    external_deps = [
      "c_utils:utils",
      "hilog_native:libhilog",
    ]

    install_images = [
      "system",
      "updater",
    ]

    output_extension = "so"
    innerapi_tags = [
      "chipsetsdk_indirect",
      "platformsdk_indirect",
    ]
    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }

  ohos_static_library("backtrace_local") {
    public_configs = [ ":dfx_backtrace_config" ]

    sources = backtrace_local_sources
    include_dirs = [
      "$faultloggerd_frameworks_path/unwinder/include",
      "$faultloggerd_path/common",
      "$faultloggerd_path/common/dfxlog",
      "$faultloggerd_path/common/dfxutil",
      "//third_party/libunwind/include",
      "//third_party/libunwind/include/tdep/",
      "//third_party/libunwind/src",
    ]

    deps = [
      "$faultloggerd_frameworks_path/unwinder:dfx_unwinder_static_src",
      "$faultloggerd_path/common/dfxlog:static_hilog_base",
      "$faultloggerd_path/interfaces/innerkits/procinfo:dfx_procinfo_static",
      "//third_party/bounds_checking_function:libsec_static",
      "//third_party/libunwind:libunwind_local",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog_native:libhilog_base",
    ]

    part_name = "faultloggerd"
    subsystem_name = "hiviewdfx"
  }
}
